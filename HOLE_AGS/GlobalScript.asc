// main global script file

// called when the game starts, before the first room is loaded
function game_start()
{
  // Auto-save on the save slot 999
  SetRestartPoint();
  SetGameSpeed(60);
  mouse.Mode = eModeInteract;
}

// called on every game cycle, except when the game is blocked
function repeatedly_execute()
{
  if (player.Room == 11) {
    RopeMainProcess();
  }
}

function on_event(int event, int data1, int data2)
{
    if (event == eEventEnterRoomBeforeFadein) {
        // do something on each room enter, before screen fades in
    } else if (event == eEventRestoreGame) {
      gScore -= 25;
      gSavedGameNow = false;
      guiSaveQuit.Visible = false;
    }
}

// called on every game cycle, even when the game is blocked
function repeatedly_execute_always()
{
  // Apply correct audio max volume settings
  if (audioAmbience != null && audioAmbience.Volume != gSettingSFXMaxVolume) {
    if (gAmbientTargetVolume > gSettingSFXMaxVolume) {
      audioAmbience.Volume = gSettingSFXMaxVolume;
    } else {
      audioAmbience.Volume = gAmbientTargetVolume;
    }
  }
  
  if (audioSFX != null && audioSFX.Volume != gSettingSFXMaxVolume) {
    audioSFX.Volume = gSettingSFXMaxVolume;
  }
   
  if (audioMusic != null && audioMusic.Volume != gSettingMusicMaxVolume) {
    audioMusic.Volume = gSettingMusicMaxVolume;
  }
  
  if (player.Room == 11) {
    gTimerFrames++;
    if (gTimerFrames >= 60) {
      gTimerFrames = 0;
      gTimerSeconds++;
    }
    if (gTimerSeconds >= 60) {
      gTimerSeconds = 0;
      gScore -= 1;
      gTimerMinutes++;
    }
    if (gTimerMinutes >= 60) {
      gTimerMinutes = 0;
      gTimerHours++;
    }
  }
    
  
  // DEBUG DISPLAY
  deLabSpeed.Text = String.Format("SPD: %f", gRopeVelocity);
  deLabTrupos.Text = String.Format("POS: %f", gPlayerPositionAccurate);
  deLabRopeleft.Text = String.Format("RLFT: %f", gRopeSpoolRemaining);
  if (gRopeLocked == false) {
    dBtnRopeLock.Visible = false;
  } else {
    dBtnRopeLock.Visible = true;
  }
  if (IsKeyPressed(eKeyUpArrow)) {deBtnU.Visible = true;} else {deBtnU.Visible = false;}
  if (IsKeyPressed(eKeyLeftArrow)) {deBtnL.Visible = true;} else {deBtnL.Visible = false;}
  if (IsKeyPressed(eKeyDownArrow)) {deBtnD.Visible = true;} else {deBtnD.Visible = false;}
  if (IsKeyPressed(eKeyRightArrow)) {deBtnR.Visible = true;} else {deBtnR.Visible = false;}
}

// called when a key is pressed
function on_key_press(eKeyCode keycode, int mod)
{
  if (IsGamePaused())
  {
    // game paused, so don't react to any keypresses
    keycode = 0;
  }
  else if (keycode == eKeyQ && (mod & eKeyModCtrl))
  {
    // Ctrl-Q will quit the game
    QuitGame(1);
  }
  else if (keycode == eKeyF5)
  {
    if (gDebug == true) {
      SaveGameSlot(1, "x");
    }
  } else if (keycode == eKeyF9) {
    if (gDebug == true && Game.GetSaveSlotDescription(1) != null) {
      RestoreGameSlot(1);
    }
  }
  else if (keycode == eKeyF12)
  {
    // F12 will save a screenshot to the save game folder
    SaveScreenShot("screenshot.pcx");
  }
  else if (mod & eKeyModCtrl)
  {
    if (keycode == eKeyD)
    {
      return; // This disables debug
      if (gDebug == true) {
        gDebug = false;
      } else {
        gDebug = true;
      }
      // Debug mode toggle
      if (guiDebug.Visible == true) {
        guiDebug.Visible = false;
      } else {
        guiDebug.Visible = true;
      }
      
    } 
  }
}

// called when a mouse button is clicked
function on_mouse_click(MouseButton button)
{
  if (IsGamePaused())
  {
    // game is paused, so do nothing (i.e. don't process mouse clicks)
  }
  else if (button == eMouseLeft)
  {
    // left-click, so try using the current mouse cursor mode at this position
    Room.ProcessClick(mouse.x, mouse.y, eModeInteract);
  }
  else if (button == eMouseRight)
  { // This does nothing :)

  }
}

function dialog_request(int param)
{

}

// MAIN MENU FUNCTIONS 
function btnMainStart_OnClick(GUIControl *control, MouseButton button)
{
  StartCutscene(eSkipAnyKeyOrMouseClick);
  audioMusic.Stop();
  int StartTransp = 0;
  while (StartTransp < 100) {
    StartTransp++;
    guiMainMenu.Transparency = StartTransp;
    Wait(1);
  }
  guiMainMenu.Visible = false;
  EndCutscene();
  player.ChangeRoom(10, 160, 160);
}

function btnMainLoad_OnClick(GUIControl *control, MouseButton button)
{
  if (Game.GetSaveSlotDescription(1) != null) {
    RestoreGameSlot(1);
  } else {
    Display("No saved game found");
  }
}

function btnMainSettings_OnClick(GUIControl *control, MouseButton button)
{
  guiMenuSettings.Visible = true;
}

function btnMainQuit_OnClick(GUIControl *control, MouseButton button)
{
  QuitGame(0);
}

// XXX

function Button2_OnClick(GUIControl *control, MouseButton button)
{
  guiSaveQuit.Visible = false;
}

function Button1_OnClick(GUIControl *control, MouseButton button)
{
  SaveGameSlot(1, "SaveQuit");
  guiSaveQuit.Visible = false;
  gSavedGameNow = true;
}

function btnScoreExit_OnClick(GUIControl *control, MouseButton button)
{
  int ScoreExitTimer = 0;
  while (ScoreExitTimer < 100) {
    ScoreExitTimer++;
    guiScoreScreen.Transparency = ScoreExitTimer;
    Wait(2);
  }
  Wait(30);
  player.ChangeRoom(20, -100, -100);
}

function Button4_OnClick(GUIControl *control, MouseButton button)
{
  guiMenuSettings.Visible = false;
  guiSaveQuit.Visible = false;
}

function Button5_OnClick(GUIControl *control, MouseButton button)
{
  if (gSettingsTutorialEnabled == true) {
    gSettingsTutorialEnabled = false;
    Button5.Text = "OFF";
  } else {
    gSettingsTutorialEnabled = true;
    Button5.Text = "ON";
  }
}


function sldSFXvolume_OnChange(GUIControl *control) {
  gSettingSFXMaxVolume = sldSFXvolume.Value;
}

function sldMusicVolume_OnChange(GUIControl *control) {
  gSettingMusicMaxVolume = sldMusicVolume.Value;
}

function btnInGameSettings_OnClick(GUIControl *control, MouseButton button)
{
  guiMenuSettings.Visible = true;
}
