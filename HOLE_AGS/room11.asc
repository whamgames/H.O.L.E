// room script file

int AnimDelayBottom = 0;
int Sparkly = 0;
int BGmovedistance = 0;

function Sparkler() {
  int SparklerPicker;
  int SparklerTries = 0;
  bool SparklerFound = false;
  int SparkRandom;
  
  while (SparklerFound == false && SparklerTries < 128) {
    SparkRandom = 4 + Random(11);
    if (object[SparkRandom].View == 4 && object[SparkRandom].Y < 320 && object[SparkRandom].Y > 0) {
      object[SparkRandom].Animate(object[SparkRandom].Loop, 1, eOnce, eNoBlock);
      SparklerFound = true;
    }
    SparklerTries++;
  }
  
}

function GenerateNewPOI() {
  // Generate 1-6 POI for the span of oWalls2 so they are ready,  alternate between set 1 and set 2 to avoid overlap
  int MaxPOI = 1;
  int MinPOI = 1;
  int POItoGenerate = 0;
  int POIgenerated = 0;
  int NextObjectIDToUse = -1;
  
  // Set max POI ranges
  if (gDepthLayer > 12) {MaxPOI++;}
  if (gDepthLayer > 24) {MaxPOI++;}
  if (gDepthLayer > 34) {MaxPOI++;}
  if (gDepthLayer > 44) {MaxPOI++;}
  // Set min POI ranges
  if (gDepthLayer > 5)  {MinPOI++;}
  if (gDepthLayer > 20) {MinPOI++;}
  if (gDepthLayer > 38) {MinPOI++;}
  
  // Randomize POI to generate for next section
  POItoGenerate = Random(MaxPOI);
  
  // Override level 3 if zero,  making sure at least 1 POI is generated in the early levels (could still be more)
  if (gDepthLayer == 3 || POItoGenerate == 0) {
    POItoGenerate = 1;
  }
  while (POItoGenerate < MinPOI) { // Ensure minimum POI count is generated
   POItoGenerate++;
  }
  
  if (gNextPOIBatch == 0) { // Batch is 0,  use objects 4-9
    gNextPOIBatch = 1;
    NextObjectIDToUse = 4;
  } else {                  // Batch is 1,  use objects 10-15
    gNextPOIBatch = 0;
    NextObjectIDToUse = 10;
  }
  
  // Generate random POI,  X coordinate range depends on depth level used
  int LeftRight;
  int XMargin = 0;
  while (POIgenerated < POItoGenerate) {
    LeftRight = Random(1); // 0 = left, 1 = right
    // Pick graphic from correct sprite pool based on direction <- TODO: Sprite pool variation
    if (LeftRight == 0) {
      object[NextObjectIDToUse].SetView(4, Random(9), 0);
    } else {
      object[NextObjectIDToUse].SetView(4, Random(9), 0);
    }
    
    // Set X coordinate based on direction and depthlevel <- TODO: Depth level variance
    if (gDepthLayer <= 5) {
      XMargin = 64;
    } else if (gDepthLayer <= 13) {
      XMargin = 80;
    } else if (gDepthLayer <= 20) {
      XMargin = 100;
    } else if (gDepthLayer <= 31) {
      XMargin = 114;
    } else if (gDepthLayer <= 45) {
      XMargin = 130;
    } else {
      XMargin = 142;
    }
    
    if (LeftRight == 0) {
      object[NextObjectIDToUse].X = 10 + Random(XMargin - 20);
    } else {
      object[NextObjectIDToUse].X = 310 - Random(XMargin - 20);
    }
    
    // Set Y coordinate within random range
    object[NextObjectIDToUse].Y = oWalls2.Y - 20 - Random(1240);
    object[NextObjectIDToUse].Name = "POI";
    // Move on to next iteration of while loop or exit
    NextObjectIDToUse++;
    //Display("POI generated: %d", POIgenerated);
    POIgenerated++;
  }
  
  // Tester, just create one simple POI for testing
  /*
  if (object[4].Y < 0) {
    object[4].Graphic = 23;
    object[4].X = 262 + Random(32);
    object[4].Y = oWalls2.Y - 120 - Random(860);
    object[4].Name = "POI";
  }*/
}

function UpdateWallSectionGraphics() {
  // We have moved down a level
  // Set oWalls1 visual and position to that of oWalls2
  oWalls1.Graphic = oWalls2.Graphic;
  oWalls2.Graphic = 0;
  oWalls1.X = 0;
  oWalls1.Y = oWalls2.Y;
  // Update oWalls2 graphic and position for next cycle
  oWalls2.X = 0;
  oWalls2.Y = oWalls1.Y + 1280;
  
  // Set appropriate graphic to represent narrowing based on depthlevel
  if (gDepthLayer <= 4) {
    oWalls2.Graphic = 12;
  } else if (gDepthLayer == 5) {
    oWalls2.Graphic = 5;
  } else if (gDepthLayer <= 12) {
    oWalls2.Graphic = 13;
  } else if (gDepthLayer == 13) {
    oWalls2.Graphic = 6;
  } else if (gDepthLayer <= 19) {
    oWalls2.Graphic = 14;
  } else if (gDepthLayer == 20) {
    oWalls2.Graphic = 7;
  } else if (gDepthLayer <= 30) {
    oWalls2.Graphic = 15;
  } else if (gDepthLayer == 31) {
    oWalls2.Graphic = 8;
  } else if (gDepthLayer <= 44) {
    oWalls2.Graphic = 16;
  } else if (gDepthLayer == 45) {
    oWalls2.Graphic = 9;
  } else if (gDepthLayer <= 51) {
    oWalls2.Graphic = 17;
  } else if (gDepthLayer == 52) {
    oWalls2.Graphic = 10;
  } else if (gDepthLayer == 53) {
    oWalls2.Graphic = 24;
  } else {
    Display("YOU SHOULD NOT BE HERE - THE GAME IS BROKEN");
    Wait(60);
    Display("INVALID gDepthLayer (%d) ERROR IN UpdateWallSectionGraphics - Please report this to the developer", gDepthLayer);
    oWalls2.Graphic = 24;
    RestartGame();
  }
  // TODO: Add story overrides here
  
  //oWalls2.Graphic = 12; // DEBUG override,  disabled when not needed
  gPlayerPositionAccurate += 1280.0;
  GenerateNewPOI();
  
  // Check for ambience
  if (gDepthLayer == 8) {
    audioAmbience = aAmbi_cavehum.Play(eAudioPriorityNormal, eRepeat);
    audioAmbience.Volume = 2;
  } else if (gDepthLayer > 8) {
    audioAmbience.Volume = audioAmbience.Volume + 2;
  }
  
  // Check music play triggers
  if (gDepthLayer == 13) {
    audioMusic = aOssuary_5___Rest.Play(eAudioPriorityHigh, eOnce);
  } else if (gDepthLayer == 20) {
    audioMusic = aLong_Note_Two.Play(eAudioPriorityHigh, eOnce);
  } else if (gDepthLayer == 31) {
    audioMusic = aSCP_x2x.Play(eAudioPriorityHigh, eOnce);
  } else if (gDepthLayer == 45) {
    audioMusic = aGiant_Wyrm.Play(eAudioPriorityHigh, eOnce);
  }
  
}


function room_Load()
{
  cEgo.Transparency = 0; // Restore player character visibility
  gPlayerPositionAccurate = 1280.0;
}

function room_AfterFadeIn()
{
  
  if (gDebug == false) {
    // TODO: Player intro drop animation,  scripted
    TriggerDialog(1);
    audioMusic = aSatiate_Strings.Play(eAudioPriorityHigh, eOnce);
  }
}

function room_RepExec()
{   
    int POIObjectYStart = 0;
    int POIObjectYEnd = 0;
    int POIObjectID = 4;

    if (gRopeSnapped == true) { // Trigger game end animation if rope has snapped
      Wait(120);
      if (gRopeSpoolRemaining <= 0.0) {
        Display("You ran out of rope and broke the rigging, causing you to fall to your death");
      } else {
        Display("You snapped the rope and fell to your death");
      }
      
      Wait(60);
      FadeOut(2);
      RestartGame();
    }
    
    Sparkly++;
    if (Sparkly > 360) {
      Sparkler();
      Sparkly = 0;
    }
    
    if (gGameEndTriggered == 1) {
      AnimDelayBottom++;
      if (AnimDelayBottom >= 40) {
        if (oWalls1.Graphic == 10) {
          oWalls1.Graphic = 25;
        } else {
          oWalls1.Graphic = 10;
        }
        AnimDelayBottom = 0 + Random(20);
      }
    }
    
    if (gGameEndTriggered == 2) {
      StartCutscene(eSkipESCOnly);
      // TODO: End cutscene
      cEye.Loop = 0;
      cJaws.Loop = 1;
      cEye.ChangeRoom(player.Room, player.x-100, oWalls1.Y-60);
      cEye.Baseline = 318;
      cJaws.Baseline = 314;
      cJaws.ChangeRoom(player.Room, 157, oWalls1.Y+40);
      Wait(60);
      cEye.Animate(0, 5, eOnce, eBlock);
      Wait(90);
      cJaws.Animate(1, 5, eOnce, eBlock);
      Wait(20);
      FadeOut(10);
      Wait(60);
      EndCutscene();
      RestartGame();
    }
    
    if (gRopeVelocity < 0.0) { // Just stopped, so animate the stop
      // TODO: Animate stop properly
      audioSFX = aClaspclack.Play(eAudioPriorityHigh, eOnce);
      player.y += 1; Wait(10);
      player.y -= 1;
      
      if (gRopeSpoolRemaining <= 0.0) { // Rope has run out!
        if (gRopeSpoolMercy == true) {
          TriggerDialog(8);
          
          gRopeSpoolRemaining = 12400.0;
          gRopeSpoolStatus = 0;
          gRopeSpoolRemaining = 12400.0;
          TriggerDialog(6);
          FadeOut(1);
          Wait(30);
          // TODO: sound effects
          oLeashBase.Y = 42;
          oLeashPole.Y = oLeashBase.Y - 29;
          FadeIn(1);
          Wait(30);
          TriggerDialog(7);
          gRopeSpoolMercy = false;
        } else {
          
          while (player.y <= 680) {
            player.y += 10;
            oRope1.Y += 10;
            Wait(1);
          }
          TriggerDialog(9);
          FadeOut(1);
          Wait(60);
          RestartGame();
        }
      } else if (gRopeVelocity < -9.0 && Game.DoOnceOnly("Speedwarning") && gDebug == false) {
        TriggerDialog(2);
      } else {
        // Check for depth specific progression dialogs
        if (gDepthLayer > 5 && Game.DoOnceOnly("DL5")) { TriggerDialog(101);
        } else if (gDepthLayer > 13 && Game.DoOnceOnly("DL13")) {TriggerDialog(102);
        } else if (gDepthLayer > 16 && Game.DoOnceOnly("DL16")) {TriggerDialog(103);
        } else if (gDepthLayer > 20 && Game.DoOnceOnly("DL20")) {TriggerDialog(104);
        } else if (gDepthLayer > 27 && Game.DoOnceOnly("DL27")) {TriggerDialog(105);
        } else if (gDepthLayer > 31 && Game.DoOnceOnly("DL31")) {TriggerDialog(106);
        } else if (gDepthLayer > 36 && Game.DoOnceOnly("DL36")) {TriggerDialog(107);
        } else if (gDepthLayer > 41 && Game.DoOnceOnly("DL41")) {TriggerDialog(108);
        } else if (gDepthLayer > 45 && Game.DoOnceOnly("DL45")) {TriggerDialog(109);
        } else if (gDepthLayer > 47 && Game.DoOnceOnly("DL47")) {TriggerDialog(110);
        } else if (gDepthLayer > 48 && Game.DoOnceOnly("DL48")) {TriggerDialog(111);
        } else if (gDepthLayer > 51 && Game.DoOnceOnly("DL50")) {TriggerDialog(112);
        }
      }
      deLabStopVel.Text = String.Format("STP: %f", gRopeVelocity);
      gRopeVelocity = 0.0;
      
      // Handle rope spool warnings
      if (gRopeSpoolRemaining < 6800.0 && gRopeSpoolStatus == 0) {
        gRopeSpoolStatus = 1;
        TriggerDialog(4);
      } else if (gRopeSpoolRemaining < 800.0 && gRopeSpoolStatus == 1) {
        gRopeSpoolStatus = 2;
        TriggerDialog(5);
      } else if (gRopeSpoolRemaining < 320.0 && gRopeSpoolStatus == 2) {
        gRopeSpoolStatus = 0;
        gRopeSpoolRemaining = 12400.0;
        TriggerDialog(6);
        FadeOut(1);
        Wait(30);
        // TODO: sound effects
        oLeashBase.Y = 42;
        oLeashPole.Y = oLeashBase.Y - 29;
        FadeIn(1);
        Wait(30);
        TriggerDialog(7);
      }
      
      return;
    }
    
    if (gRopeVelocity > 0.0) { // Not stopped and we have velocity, so calculate new position and animate wall Y movement
      if (player.y < 160) {
        player.y += 2;
        oRope1.Y += 2;
      }
      gPlayerPositionAccurate -= gRopeVelocity / 1.0;
      gRopeSpoolRemaining -= gRopeVelocity / 1.0;
      POIObjectYStart = oWalls1.Y;
      oWalls1.Y = FloatToInt(gPlayerPositionAccurate, eRoundNearest);
      POIObjectYEnd = oWalls1.Y;
      
      if (POIObjectYStart != POIObjectYEnd) { // Walls have moved up, so update POI positions to match
        int POIMoveDist = POIObjectYStart - POIObjectYEnd;
        BGmovedistance += POIMoveDist;
        while (POIObjectID <= 12) { // Move all objects attached to the walls here
          object[POIObjectID].Y = object[POIObjectID].Y - POIMoveDist;
          POIObjectID++;
        }
        oLeashBase.Y = oLeashBase.Y - POIMoveDist;
        oLeashPole.Y = oLeashPole.Y - POIMoveDist;
        
        while (BGmovedistance > 32) {
          BGmovedistance -= 32;
          oRasterBG.Y -= 1;
        }
        
      }
      
      if (oWalls1.Y > -640) {
        oWalls2.Y = oWalls1.Y + 1280;
      } else {
        UpdateWallSectionGraphics();
        gDepthLayer += 1;
        deLabLevel.Text = String.Format("LVL: %d", gDepthLayer);
      }
      return;
    }
    
    if (gClimbToProcess == true) {
      gClimbToProcess = false;
      player.y -= 1;
      oRope1.Y -= 1;
      
      if (player.y < 20) {
        // TODO: convert to proper dialog call
        Wait(60);
        cRadio.SayAt(140, 30, 160, "What's up? You ready to come on up to the surface?");
        if (gScore < 5) {
          cRadio.SayAt(140, 30, 160, "We haven't really gotten a whole lot of data from down there");
          cRadio.SayAt(140, 30, 160, "We'll definitely need you to go back again for more if we're going to make this expedition work");
        } else if (gScore < 15) {
          cRadio.SayAt(140, 30, 160, "We can probably work with this amount of specimes");
          cRadio.SayAt(140, 30, 160, "But we'll probably going to want to go deeper next time");
        } else {
          cRadio.SayAt(140, 30, 160, "Not bad, we've got somee good specimens to go over");
          cRadio.SayAt(140, 30, 160, "Seems we find more valuable data the deeper we go, though");
        }
        cRadio.SayAt(140, 30, 160, "You sure you want to wrap up?");
        // TODO: Prompt game end
        player.Say("Yeah, I think I'm done here");
        cRadio.SayAt(140, 30, 160, "All right, let's get you hauled up from there");
        Wait(120);
        FadeOut(2);
        RestartGame();
      }
    }
}
