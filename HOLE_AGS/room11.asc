// room script file

int AnimDelayBottom = 0;

function GenerateNewPOI() {
  // Generate 1-6 POI for the span of oWalls2 so they are ready,  alternate between set 1 and set 2 to avoid overlap
  int MaxPOI = 1;
  int MinPOI = 1;
  int POItoGenerate = 0;
  int POIgenerated = 0;
  int NextObjectIDToUse = -1;
  
  // Set max POI ranges
  if (gDepthLayer > 12) {MaxPOI++;}
  if (gDepthLayer > 24) {MaxPOI++;}
  if (gDepthLayer > 34) {MaxPOI++;}
  if (gDepthLayer > 44) {MaxPOI++;}
  // Set min POI ranges
  if (gDepthLayer > 5)  {MinPOI++;}
  if (gDepthLayer > 20) {MinPOI++;}
  if (gDepthLayer > 38) {MinPOI++;}
  
  // Randomize POI to generate for next section
  POItoGenerate = Random(MaxPOI);
  
  // Override level 3 if zero,  making sure at least 1 POI is generated in the early levels (could still be more)
  if (gDepthLayer == 3 || POItoGenerate == 0) {
    POItoGenerate = 1;
  }
  while (POItoGenerate < MinPOI) { // Ensure minimum POI count is generated
   POItoGenerate++;
  }
  
  if (gNextPOIBatch == 0) { // Batch is 0,  use objects 4-9
    gNextPOIBatch = 1;
    NextObjectIDToUse = 4;
  } else {                  // Batch is 1,  use objects 10-15
    gNextPOIBatch = 0;
    NextObjectIDToUse = 10;
  }
  
  // Generate actually random POI,  X coordinate range depends on depth level used
  int LeftRight;
  int XMargin = 0;
  while (POIgenerated < POItoGenerate) {
    LeftRight = Random(1); // 0 = left, 1 = right
    // Pick graphic from correct sprite pool based on direction <- TODO: Sprite pool variation
    if (LeftRight == 0) {
      object[NextObjectIDToUse].Graphic = 22;
    } else {
      object[NextObjectIDToUse].Graphic = 23;
    }
    
    // Set X coordinate based on direction and depthlevel <- TODO: Depth level variance
    if (gDepthLayer <= 5) {
      XMargin = 70;
    } else if (gDepthLayer <= 13) {
      XMargin = 82;
    } else if (gDepthLayer <= 20) {
      XMargin = 100;
    } else if (gDepthLayer <= 31) {
      XMargin = 114;
    } else if (gDepthLayer <= 45) {
      XMargin = 130;
    } else {
      XMargin = 142;
    }
    
    if (LeftRight == 0) {
      object[NextObjectIDToUse].X = 10 + Random(XMargin - 20);
    } else {
      object[NextObjectIDToUse].X = 310 - Random(XMargin - 20);
    }
    
    // Set Y coordinate within random range
    object[NextObjectIDToUse].Y = oWalls2.Y - 20 - Random(1240);
    object[NextObjectIDToUse].Name = "POI";
    // Move on to next iteration of while loop or exit
    NextObjectIDToUse++;
    //Display("POI generated: %d", POIgenerated);
    POIgenerated++;
  }
  
  // Tester, just create one simple POI for testing
  /*
  if (object[4].Y < 0) {
    object[4].Graphic = 23;
    object[4].X = 262 + Random(32);
    object[4].Y = oWalls2.Y - 120 - Random(860);
    object[4].Name = "POI";
  }*/
}

function UpdateWallSectionGraphics() {
  // We have moved down a level
  // Set oWalls1 visual and position to that of oWalls2
  oWalls1.Graphic = oWalls2.Graphic;
  oWalls2.Graphic = 0;
  oWalls1.X = 0;
  oWalls1.Y = oWalls2.Y;
  // Update oWalls2 graphic and position for next cycle
  oWalls2.X = 0;
  oWalls2.Y = oWalls1.Y + 1280;
  
  // Set appropriate graphic to represent narrowing based on depthlevel
  if (gDepthLayer <= 4) {
    oWalls2.Graphic = 12;
  } else if (gDepthLayer == 5) {
    oWalls2.Graphic = 5;
  } else if (gDepthLayer <= 12) {
    oWalls2.Graphic = 13;
  } else if (gDepthLayer == 13) {
    oWalls2.Graphic = 6;
  } else if (gDepthLayer <= 19) {
    oWalls2.Graphic = 14;
  } else if (gDepthLayer == 20) {
    oWalls2.Graphic = 7;
  } else if (gDepthLayer <= 30) {
    oWalls2.Graphic = 15;
  } else if (gDepthLayer == 31) {
    oWalls2.Graphic = 8;
  } else if (gDepthLayer <= 44) {
    oWalls2.Graphic = 16;
  } else if (gDepthLayer == 45) {
    oWalls2.Graphic = 9;
  } else if (gDepthLayer <= 51) {
    oWalls2.Graphic = 17;
  } else if (gDepthLayer == 52) {
    oWalls2.Graphic = 10;
  } else if (gDepthLayer == 53) {
    oWalls2.Graphic = 24;
  } else {
    Display("YOU SHOULD NOT BE HERE - THE GAME IS BROKEN");
    Wait(60);
    Display("INVALID gDepthLayer (%d) ERROR IN UpdateWallSectionGraphics - Please report this to the developer", gDepthLayer);
    oWalls2.Graphic = 24;
    RestartGame();
  }
  // TODO: Add story overrides here
  
  //oWalls2.Graphic = 12; // DEBUG override,  disabled when not needed
  gPlayerPositionAccurate += 1280.0;
  GenerateNewPOI();
}


function room_Load()
{
  cEgo.Transparency = 0; // Restore player character visibility
  gPlayerPositionAccurate = 1280.0;
}

function room_AfterFadeIn()
{
  
  if (gDebug == true) {
    // TODO: Player intro drop animation,  scripted
    TriggerDialog(1);
  }
}

function room_RepExec()
{   
    int POIObjectYStart = 0;
    int POIObjectYEnd = 0;
    int POIObjectID = 4;
    if (gRopeSnapped == true) { // Trigger game end animation if rope has snapped
      Wait(120);
      Display("You snapped the rope and fell to your death");
      Wait(60);
      FadeOut(2);
      RestartGame();
    }
    
    if (gGameEndTriggered == 1) {
      AnimDelayBottom++;
      if (AnimDelayBottom >= 40) {
        if (oWalls1.Graphic == 10) {
          oWalls1.Graphic = 25;
        } else {
          oWalls1.Graphic = 10;
        }
        AnimDelayBottom = 0 + Random(20);
      }
    }
    
    if (gGameEndTriggered == 2) {
      StartCutscene(eSkipESCOnly);
      // TODO: End cutscene
      cEye.Loop = 0;
      cJaws.Loop = 1;
      cEye.ChangeRoom(player.Room, player.x-100, oWalls1.Y-60);
      cEye.Baseline = 318;
      cJaws.Baseline = 314;
      cJaws.ChangeRoom(player.Room, 157, oWalls1.Y+40);
      Wait(60);
      cEye.Animate(0, 5, eOnce, eBlock);
      Wait(90);
      cJaws.Animate(1, 5, eOnce, eBlock);
      Wait(20);
      FadeOut(10);
      Wait(60);
      EndCutscene();
      RestartGame();
    }
    
    if (gRopeVelocity < 0.0) { // Just stopped, so animate the stop
      // TODO: Animate stop properly
      player.y += 1; Wait(10);
      player.y -= 1;
      
      if (gRopeVelocity < -9.0 && Game.DoOnceOnly("Speedwarning") && gDebug == false) {
        TriggerDialog(2);
      }
      deLabStopVel.Text = String.Format("STP: %f", gRopeVelocity);
      gRopeVelocity = 0.0;
      return;
    }
    
    if (gRopeVelocity > 0.0) { // Not stopped and we have velocity, so calculate new position and animate wall Y movement
      gPlayerPositionAccurate -= gRopeVelocity / 1.0;
      POIObjectYStart = oWalls1.Y;
      oWalls1.Y = FloatToInt(gPlayerPositionAccurate, eRoundNearest);
      POIObjectYEnd = oWalls1.Y;
      
      if (POIObjectYStart != POIObjectYEnd) { // Walls have moved up, so update POI positions to match
        int POIMoveDist = POIObjectYStart - POIObjectYEnd;
        while (POIObjectID <= 12) {
          object[POIObjectID].Y = object[POIObjectID].Y - POIMoveDist;
          POIObjectID++;
        }
      }
      
      if (oWalls1.Y > -640) {
        oWalls2.Y = oWalls1.Y + 1280;
      } else {
        UpdateWallSectionGraphics();
        gDepthLayer += 1;
        deLabLevel.Text = String.Format("LVL: %d", gDepthLayer);
      }
      return;
    }
    
    if (gClimbToProcess == true) {
      gPlayerPositionAccurate += 1.0;
      gClimbToProcess = false;
      gClimbedSteps += 1;
      
      POIObjectYStart = oWalls1.Y;
      oWalls1.Y = FloatToInt(gPlayerPositionAccurate, eRoundNearest);
      POIObjectYEnd = oWalls1.Y;

      oWalls2.Y = oWalls1.Y + 1280;
      
      if (POIObjectYStart != POIObjectYEnd) { // Walls have moved up, so update POI positions to match
        int POIMoveDist = POIObjectYStart - POIObjectYEnd;
        while (POIObjectID <= 12) {
          object[POIObjectID].Y = object[POIObjectID].Y - POIMoveDist;
          POIObjectID++;
        }
      }
      
      if (gClimbedSteps > 240) {
        Wait(60);
        cRadio.Say("What's up? You ready to come on up to the surface?");
        if (gScore < 5) {
          cRadio.Say("We haven't really gotten a whole lot of data from down there");
        } else {
          cRadio.Say("Not bad, we've got somee good specimens to go over");
        }
        cRadio.Say("You sure you want to wrap up?");
        // TODO: Prompt game end
        player.Say("Yeah, I think I'm done here");
        Wait(120);
        FadeOut(2);
        RestartGame();
      }
    }
}
