// room script file

float PlayerPositionAccurate = 1280.0;

function GenerateNewPOI() {
  // Generate 1-6 POI for the span of oWalls2 so they are ready,  alternate between set 1 and set 2 to avoid overlap
  int MaxPOI = 1;
  int MinPOI = 1;
  int POItoGenerate = 0;
  int POIgenerated = 0;
  int NextObjectIDToUse = -1;
  
  // Set max POI ranges
  if (gDepthLayer > 12) {MaxPOI++;}
  if (gDepthLayer > 24) {MaxPOI++;}
  if (gDepthLayer > 34) {MaxPOI++;}
  if (gDepthLayer > 44) {MaxPOI++;}
  // Set min POI ranges
  if (gDepthLayer > 5)  {MinPOI++;}
  if (gDepthLayer > 20) {MinPOI++;}
  if (gDepthLayer > 38) {MinPOI++;}
  
  // Randomize POI to generate for next section
  POItoGenerate = Random(MaxPOI);
  while (POItoGenerate < MinPOI) { // Ensure minimum POI count is generated
   POItoGenerate++;
  }
  
  if (gNextPOIBatch == 0) { // Batch is 0,  use objects 4-9
    gNextPOIBatch = 1;
    NextObjectIDToUse = 4;
  } else {                  // Batch is 1,  use objects 10-15
    gNextPOIBatch = 0;
    NextObjectIDToUse = 10;
  }
  
  // Generate actually random POI,  X coordinate range depends on depth level used
  int LeftRight;
  while (POIgenerated < POItoGenerate) {
    LeftRight = Random(1); // 0 = left, 1 = right
    // Pick graphic from correct sprite pool based on direction <- TODO: Sprite pool variation
    if (LeftRight == 0) {
      object[NextObjectIDToUse].Graphic = 22;
    } else {
      object[NextObjectIDToUse].Graphic = 23;
    }
    
    // Set X coordinate based on direction and depthlevel <- TODO: Depth level variance
    if (LeftRight == 0) {
      object[NextObjectIDToUse].X = 20 + Random(40);
    } else {
      object[NextObjectIDToUse].X = 300 - Random(40);
    }
    
    // Set Y coordinate within random range
    object[NextObjectIDToUse].Y = oWalls2.Y - 20 - Random(1240);
    object[NextObjectIDToUse].Name = "POI";
    // Move on to next iteration of while loop or exit
    NextObjectIDToUse++;
    //Display("POI generated: %d", POIgenerated);
    POIgenerated++;
  }
  
  // Tester, just create one simple POI for testing
  /*
  if (object[4].Y < 0) {
    object[4].Graphic = 23;
    object[4].X = 262 + Random(32);
    object[4].Y = oWalls2.Y - 120 - Random(860);
    object[4].Name = "POI";
  }*/
}

function UpdateWallSectionGraphics() {
  // We have moved down a level
  // Set oWalls1 visual and position to that of oWalls2
  oWalls1.Graphic = oWalls2.Graphic;
  oWalls2.Graphic = 0;
  oWalls1.X = 0;
  oWalls1.Y = oWalls2.Y;
  // Update oWalls2 graphic and position for next cycle
  oWalls2.X = 0;
  oWalls2.Y = oWalls1.Y + 1280;
  oWalls2.Graphic = 12;
  PlayerPositionAccurate += 1280.0;
  GenerateNewPOI();
}


function room_Load()
{
  cEgo.Transparency = 0; // Restore player character visibility
}

function room_AfterFadeIn()
{
  Display("You have entered the game!");
  // TODO: Player intro drop animation,  scripted
  cRadio.Transparency = 100;
  cRadio.x = player.x - 100;
  cRadio.y = player.y - 280;
  cRadio.Say("Testing 1, 2, 3");
  cRadio.Say("Can you hear me okay down there?");
  player.Say("I hear you");
  player.Say("Heading deeper now, just keep an eye on things for me");
  cRadio.Say("Don't you worry, just try not to straing the rope too much with any hard stops");
}

function room_RepExec()
{   
    int POIObjectYStart = 0;
    int POIObjectYEnd = 0;
    int POIObjectID = 4;
    if (gRopeSnapped == true) { // Trigger game end animation if rope has snapped
      Wait(120);
      Display("You snapped the rope and fell to your death");
      Wait(60);
      FadeOut(2);
      RestartGame();
    }
    
    if (gRopeVelocity < 0.0) { // Just stopped, so animate the stop
      // TODO: Animate stop
      player.y += 1; Wait(10);
      player.y -= 1;
      
      if (gRopeVelocity < -9.0 && Game.DoOnceOnly("Speedwarning")) {
        cRadio.Say("Whoooah!");
        Wait(60);
        player.Say("What was that?");
        cRadio.Say("Lots of strain on the rope just now");
        cRadio.Say("Did you do a hard stop at high speed or something?");
        Wait(60);
        player.Say("I might have been a little fast there");
        cRadio.Say("The rope can only handle so much, not to mention the old rigging up here");
        cRadio.Say("Don't push your luck too much, take it nice and slow, okay?");
        player.Say("I'll try");
        player.Say("It's a long way down, though, takes ages to find anything!");
      }
      deLabStopVel.Text = String.Format("STP: %f", gRopeVelocity);
      gRopeVelocity = 0.0;
    }
    
    if (gRopeVelocity > 0.0) { // Not stopped and we have velocity, so calculate new position and animate wall Y movement
      PlayerPositionAccurate -= gRopeVelocity / 1.0;
      POIObjectYStart = oWalls1.Y;
      oWalls1.Y = FloatToInt(PlayerPositionAccurate, eRoundNearest);
      POIObjectYEnd = oWalls1.Y;
      
      if (POIObjectYStart != POIObjectYEnd) { // Walls have moved up, so update POI positions to match
        int POIMoveDist = POIObjectYStart - POIObjectYEnd;
        while (POIObjectID <= 12) {
          object[POIObjectID].Y = object[POIObjectID].Y - POIMoveDist;
          POIObjectID++;
        }
      }
      
      if (oWalls1.Y > -640) {
        oWalls2.Y = oWalls1.Y + 1280;
      } else {
        UpdateWallSectionGraphics();
        gDepthLayer += 1;
        deLabLevel.Text = String.Format("LVL: %d", gDepthLayer);
      }
    }
}
